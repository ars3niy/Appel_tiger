let
	type ints = array of int
	var maxn := 100

	function printint(i: int) =
		let
			function f(i:int) =
				if i>0 then (
					f(i/10);
					print(chr( i - i/10*10 + ord("0")))
				)
		in
			if i<0 then (
				print("-");
				f(-i)
			) else if i>0 then
				f(i)
			else
				print("0")
		end

	function mod(a: int, b: int): int =
		a - a/b * b

	function isprime(x: int, primes: ints): int =
		let
			var result: int := 1
			var p := 0
			var i := 0
		in
			while 1 do (
				p := primes[i];
				if (p <= 0) | (p*p > x) then break;
				if x = x/p*p then (
					result := 0;
					break
				);
				i := i+1
			);
			result
		end
	var primes: ints := ints[maxn] of 0
	var nprime := 0
in
	for x := 2 to maxn do
		if isprime(x, primes) then (
			primes[nprime] := x;
			nprime := nprime+1
		);
	for i := 0 to maxn do (
		if primes[i] <= 0 then break;
		print(" ");
		printint(primes[i])
	);
	print("\n")
end
